plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    ndkPath '/data/data/com.termux/files/home/android-ndk-r23'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.0"

        consumerProguardFiles "consumer-rules.pro"

        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
            }
        }
    }

    externalNativeBuild {
        cmake {
            version '3.18.5+'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.github.itsrohit'
                artifactId 'stackblur'
                version '1.0.0'

                pom {
                    name = 'StackBlur'
                    packaging = 'aar'
                    description = 'Android stackblur (Native method)'
                    url = 'https://github.com/RohitVermaOP/StackBlur'

                    scm {
                        connection = 'https://github.com/RohitVermaOP/StackBlur.git'
                        developerConnection = 'https://github.com/RohitVermaOP/StackBlur.git'
                        url = 'https://github.com/RohitVermaOP/StackBlur'
                    }

                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'RohitVermaOP'
                            name = 'Rohit Verma'
                            email = 'rohitkumar882333@gmail.com'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "StackBlur"
                url = uri("https://maven.pkg.github.com/RohitVermaOP/StackBlur")

                credentials {
                    username = localProperties.getProperty('gpr.usr')
                    password = localProperties.getProperty('gpr.key')
                }
            }
        }
    }
}
